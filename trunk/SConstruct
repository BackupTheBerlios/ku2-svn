#
#	SConstruct file for Kane Utilities 2
#
#	Fri Apr  7 17:16:57 2006
#		J. Anton, Copyright 2006
#

env = Environment(CPPPATH = ["."])

###
#	Platform depended variables and actions
###
if env["PLATFORM"] == "win32":
	env.Append(CCFLAGS = ["/W3", "/MD", "/Wp64"], CPPDEFINES = ["_CRT_SECURE_NO_DEPRECATE", "WIN32"])
	debug_ccflags = ["/ZI"]
	release_ccflags = ["/O2"]
	release_linkflags = [""]
	sdl_includes = ["D:\devel\Microsoft Visual Studio 8\VC\include\SDL"]
elif env["PLATFORM"] == "posix":
	env.Append(CCFLAGS = ["-Wall"])
	debug_ccflags = ["-g"]
	release_ccflags = ["-O2"]
	release_linkflags = ["-s"]
	sdl_includes = ["/usr/include/SDL"]
else:
	print "Unsupported platform: " + env["PLATFORM"]
	Exit()

###
#	Checking header files
###
conf = Configure(env)
if not conf.CheckCHeader("ctype.h"):
	Exit()	
if not conf.CheckCHeader("errno.h"):
	Exit()
if not conf.CheckCHeader("libintl.h"):
	env.Append(CPPDEFINES = ["NO_LIBINTL_H"])
if not conf.CheckCHeader("stdarg.h"):
	Exit()
if not conf.CheckCHeader("stdint.h"):
	env.Append(CPPDEFINES = ["NO_STDINT_H"])
	if not conf.CheckCHeader("inttypes.h"):
		env.Append(CPPDEFINES = ["NO_INTTYPES_H"])
if not conf.CheckCHeader("stdio.h"):
	Exit()
if not conf.CheckCHeader("stdlib.h"):
	Exit()
if not conf.CheckCHeader("string.h"):
	Exit()
if not conf.CheckCHeader("time.h"):
	Exit()
env = conf.Finish()

###
#	Getting building mode
###
mode = ARGUMENTS.get("mode", "debug")
if mode == "debug":
	env.Append(CCFLAGS = debug_ccflags, CPPDEFINES = ["DEBUG"])
	debug = ARGUMENTS.get("debug", "memory,func,log")
	if "memory" in debug:
		env.Append(CPPDEFINES = ["DEBUG_MEMORY"])
	if "func" in debug:
		env.Append(CPPDEFINES = ["DEBUG_FUNC"])
	if "log" in debug:
		env.Append(CPPDEFINES = ["DEBUG_LOG"])
elif mode == "release":
	env.Append(CCFLAGS = release_ccflags)
	env.Append(LINKFLAGS = release_linkflags)
	debug = ""
else:
	print "Invalid mode ( debug|release )"
	Exit()

###
#	Getting list of add-ons to build with.
###
use = ARGUMENTS.get("use", "gettext")
notuse = ARGUMENTS.get("notuse", "")
if "gettext" in use and not "gettext" in notuse:
	env.Append(CPPDEFINES = ["USE_GETTEXT"])

###
#	Kane Utilities 2
###
sources = Split("""
	ku2/ku2.c
	log/log.c
	other/other.c
	list/dl_list.c
	abtree/abtree.c
	cfgreader/cfg.c
	resmanager/res.c
	""")

ku2lib = env.Library("ku2", sources)

###
#	Kane Utilities 2 SDL GUI and multimedia
###	
mm_sources = Split("""
	mm/gfx/gfx.c
	mm/gui/gui.c
	""")

env4mm = env.Copy()
env4mm.Append(CPPPATH = sdl_includes)

ku2mm = env4mm.Library("ku2mm", mm_sources)

###
#	Getting list of modules to build
###
build = ARGUMENTS.get("build", "core,mm")
notbuild = ARGUMENTS.get("notbuild", "")
if "core" in build and not "core" in notbuild:
	Default(ku2lib)
if "mm" in build and not "mm" in notbuild:
	Default(ku2mm)
